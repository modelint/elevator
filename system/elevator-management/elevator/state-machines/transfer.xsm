metadata
    Title : Transfer State Machine Diagram
    Author : Leon Starr
    Document ID : mint.elevator3.td.5
    Modification date : November 2, 2025
    Version : 3.5.0
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Elevator Management
class Transfer
events
    Execute
    Dest change request
    Ready to go
    Cabin dispatched
    Stationary update
    Redirect requested
    Cabin redirected
    Update in motion
    Cabin at destination
    Reversal checked
    Call in progress
    Wait to clear
    Requests cleared
    Door closed
--
initial transitions
    Execute > WAITING FOR CABIN
--
state WAITING FOR CABIN
activity
    // Cabin enroute
transitions
    Ready to go > Dispatching cabin
    Dest change request > Changing destination before cabin moves
--
state Changing destination before cabin moves( new dest: Level Name )
activity
    // The cabin isnâ€™t moving, so we can safely
    //     relink to the new destination
    &R53 Accessible Shaft Level(Floor: ^new dest; Shaft: Shaft)
    Stationary update -> me
transitions
    Stationary update > WAITING FOR CABIN
--
state Dispatching cabin
activity
    Go -> /R53/Cabin
    Cabin dispatched -> me
transitions
    Cabin dispatched > CABIN IN MOTION
--
state CABIN IN MOTION
activity
    // Cabin in transit
transitions
    Dest change request > Request redirect
    Cabin redirected > Match new destination of cabin
    Cabin at destination > Check for cabin reversal
--
state Request redirect( new dest: Level Name )
activity
    Try redirect( ^new dest ) -> /R53/Cabin
    Redirect requested -> me
transitions
    Redirect requested > CABIN IN MOTION
--
state Match new destination of cabin( updated dest: Level Name )
activity
    &R53 Accessible Shaft Level(Floor: ^updated dest; Shaft: Shaft)
    Update in motion -> me
transitions
    Update in motion > CABIN IN MOTION
--
state Check for cabin reversal
activity
    // If the Cabin arrives and there is a floor request in the opposite direction
    // and no stop request or call in the Cabinâ€™s current travel direction,
    // we must reverse the cabinâ€™s travel direction
    //
    call behind .= /R53/Accessible Shaft Level/R49/Floor Service(1, Direction: /R53/Cabin.Travel direction.opposite )
    call fwd .= /R53/Accessible Shaft Level/R49/Floor Service(1, Direction: /R53/Cabin.Travel direction )
    switch dir = call behind and !call fwd and !/R53/Accessible Shaft Level.Stop requested
    switch dir? /R53/Cabin.Travel direction.toggle

    Reversal checked -> me
transitions
    Reversal checked > Check for active floor service
--
state Check for active floor service
activity
        Cabin arrived( Shaft, direction: /R53/Cabin.Travel direction ) -> ~UI
        pending floor service .= /R53/Accessible Shaft Level/R49/Floor Service(1, Direction: /R53/Cabin.Travel direction )

        pending floor service? {
            Call in progress -> me
            Cabin arrived -> pending floor service
        } : {
            Stop serviced -> /R53/Accessible Shaft Level
            Wait to clear -> me
        }
transitions
    Call in progress > CALL IN PROGRESS
    Wait to clear > WAITING FOR REQUESTS TO CLEAR
--
state CALL IN PROGRESS
activity
    // Floor call pending
transitions
    Door closed > Canceling floor service
--
state Canceling floor service
activity
        Cancel -> /R53/Accessible Shaft Level/R49/Floor Service( Direction: /R53/Cabin.Travel direction )
        Wait to clear -> me
transitions
    Wait to clear > WAITING FOR REQUESTS TO CLEAR
--
state WAITING FOR REQUESTS TO CLEAR
activity
    // Requests still pending
transitions
    Requests cleared > Delete
--
state Delete !*
activity
        Transfer completed -> R53( /R53/Cabin/R2/Shaft )
--