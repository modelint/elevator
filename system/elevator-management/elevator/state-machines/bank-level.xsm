metadata
    Title : Bank Level State Machine Diagram
    Author : Leon Starr
    Document ID : mint.elevator3.td.5
    Creation date : November 4, 2025
    Modification date : January 12, 2026
    Version : 0.2.2
    Organization : Model Integration, LLC
    Copyright notice > MIT
    Organization logo > mint
domain Elevator Management
class Bank Level
events
    Floor requested
    Cabin selected
    Clear floor request
    Request cleared
--
state WAITING FOR CALL
activity
transitions
    Floor requested > Select cabin
    Clear floor request > Clear request
--
state Select cabin( dir: Direction )
activity
    R38? {
        _Top Bank Level:
            ^dir == _down?
                /R38/Top Bank Level.Calling down.set

        _Bottom Bank Level:
            ^dir == _up?
                /R38/Bottom Bank Level.Calling up.set

        _Middle Bank Level:
            ^dir? {
                _up: /R38/Middle Bank Level.Calling up.set
                _down: /R38/Middle Bank Level.Calling down.set
            }
    }
    Floor calling( ^dir ) -> Accessible Shaft Level(Bank; Floor; Shaft: .Choose shaft(calling floor: Floor, service dir: ^dir) )
    Cabin selected -> me
transitions
    Cabin selected > WAITING FOR CALL
--
state Clear request( dir: Direction )
activity
    R38? {
        _Top Bank Level:
            /R38/Top Bank Level.Calling down.unset
        _Bottom Bank Level:
            /R38/Bottom Bank Level.Calling up.unset
        _Middle Bank Level:
            ^dir? {
                _up: /R38/Middle Bank Level.Calling up.unset
                _down: /R38/Middle Bank Level.Calling down.unset
            }
    }

    Clear floor request( Bank, Floor, ^dir ) -> ~UI
transitions
    Request cleared > WAITING FOR CALL
--