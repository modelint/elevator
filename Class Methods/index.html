<!DOCTYPE html>
<html>
	<head>
		<meta name="viewport" content="width=device-width, initial-scale=1.0">
		<meta charset="utf-8" />
		<link rel="stylesheet" type="text/css" href="css/style.css" />
		<title>Elevator Management Class Methods</title>
	</head>
<body>
<h1>Elevator Management Class Methods</h1>

<h3>Author</h3>

<p>Leon Starr</p>

<h3>Document</h3>

<p>mint.ev3.td.13 / Version 3.3.0 / November 6, 2017</p>

<h3>Document organization</h3>

<p>Class methods are organized alphabetically by class, each starting on its own page.</p>

<h3>License / Copyright</h3>

<p>Copyright (c) 2017, Leon Starr</p>

<p>Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the &quot;Software&quot;), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:</p>

<p>The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.</p>

<p>THE SOFTWARE IS PROVIDED &quot;AS IS&quot;, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.</p>

<h1>Cabin Class Methods</h1>

<h2>Ping both ways</h2>

<pre><code>//
// Cabin.Ping both ways() : Acessible Shaft Level
//
// Search ahead of the Cabin in the indicated direction
// return the nearest ASL with a stop request and/or a Floor Service calling
// in the travel direction. If there are none, return the furthest ASL with a Floor Service
// calling opposite the travel direction if any can be found. Otherwise, just return
// an empty set.
//


// Search in the Cabin&#39;s current travel direction
fwd dest .= Ping( dir: Travel direction )
[!found] =&gt;&gt; fwd dest
[found] {
    rev dest .= Ping( dir: Travel direction.opposite )
    [!found] Travel direction.toggle
    =&gt;&gt; rev dest // Empty if there is no destination in either direction
}

// Scrall note: I&#39;m trying to move away from if-then&#39;s as much as possible to
// encourage DFD thinking. Still working on defining a consistent set of names
// for the zero, one, many case. [found] = [!empty] = [one]
// A guard is always set relative to the nearest action, so the first [!found] is
// relative to the first Ping while the second is relative to the second Ping</code></pre>

<h2>Ping</h2>

<pre><code>//
// Cabin.Ping( dir: Direction ) : Acessible Shaft Level
//
// Search ahead of the Cabin in the indicated direction
// return the nearest ASL with a stop request and/or a Floor Service calling
// in that direction. If none is found, return the empty set.
//

// Get all bank levels between cabin and end of shaft in the indicated direction
current floor number = /R43/R28/Floor.Number // Need ordinal, not floor name
case ( in.dir ) {
.up:
    aslevs ahead of cabin ..= Accessible Shaft Level( 
        /R3/R28/Floor.Number: &gt;= current floor number, Bank: Bank
    )
.down:
    aslevs ahead of cabin ..= Accessible Shaft Level( 
        /R3/R28/Floor.Number: &lt; current floor number, Bank: Bank
    )
}

// Return the nearest floor ahead of the cabin with a stop requested or a floor service call
// in the indicated direction
=&gt;&gt; aslevs ahead of cabin( 
        -^Floor.distance(its.Floor) AND
        (Stop requested OR /R49/Floor Service.Direction : in.dir )
    )

// The Ordinal type&#39;s distance operation returns the absolute value of the difference</code></pre>

<h2>Update location</h2>

<pre><code>//
// Cabin.Update location( floor : Level Name )
//
// Updates the current location of the cabin as it moves
// and notifies the UI of the new location

&amp;R43 /R2/R28/Shaft Level( in.Floor ) ; UI.New location( Shaft, floor: Current floor )

// ; symbol is shorthand for typing a guard twice
// for simple action sequences
// longhand form is
//     do x first [done]
//     [done] do y</code></pre>

<h2>Estimate delay</h2>

<pre><code>//
// Cabin.Estimate delay( calling floor : Floor name, service dir : Direction ) : Duration
//
// Returns the distance in floor units from this Cabin to the specified floor
// The algorithm is described and illustrated in the
// Bank Select technical note. mint.elevator.tn.6

// Get start, destination and direction data
//
calling fnum, calling height = Floor( Name: in.calling floor ).(Number, Height)
cabin fnum, cabin height = /R43/R28/Floor.(Number, Height)
top fnum = /R2/Shaft/Floor(+^Number) // max operator
bottom fnum = Ordinal.least // operation on Ordinal type
floor gap = Building.Average floor gap // Singleton selection, no restriction clause

// Useful guards
[calling up][calling down] = (in.service dir == .up)
[cabin high][cabin low] = (cabin fnum &gt; calling fnum)
// sets true-false if true and vice versa

// Test the simple case
if (calling fnum == cabin fnum)
    =&gt;&gt; _0.0 // distance is zero

// Two key factors determine the number of reversals required to reach
// the calling floor:
//     1) Which side of the calling arrow are we at?
//     2) Is our travel direction the same as the calling arrow?
//
// Mark high/low coordinates on shaft
// and relative cabin/calling floor position
//

[behind the call arrow] = ( [cabin low] and [calling up] ) or ( [cabin high] and [calling down] )
// single guard on LHS is set to true or false

[dir match] = (Travel direction == in.service dir)

// All shaft levels accessible in cabin&#39;s shaft with floor numbers
// --- numbered aslevs ---
// | Floor | Shaft | Bank | Stop Requested | Number |
// ---
numbered aslevs #= /R2/R28/R3/Accessible Shaft Level ## Floor.(Name &gt;&gt; Floor, Number)

[dir match][behind the call arrow] {
    //
    // Case 1: Cabin is moving in the same direction and behind call arrow tail
    // ..[cab-&gt;]....(call-&gt;)

    // Route:
    // 
    // Simplest route, it&#39;s a straight shot:
    //
    //   a&gt; Toward call in correct direction

    // Count stops and calls in the Cabin&#39;s travel direction only (ignore reverse calls).
    // Don&#39;t count the cabin or calling floor

    countable aslevs ..= numbered aslevs(Number: cabin fnum&lt;&gt;calling fnum)
    stops = Count stops oneway(aslevs: countable aslevs, search dir: Travel direction)
    distance = cabin height.separation(calling height)
}

[dir match][!behind the call arrow] {
    //
    // Case 2: Cabin is moving in the same direction, but is ahead of the call arrow
    // ..(call-&gt;)....[cab-&gt;]
    // 
    // Route: 
    // 
    // We&#39;ll have to take the long way around proceeding:
    //
    //   a&gt; to turn 1 (top or bottom of shaft) and back
    //   b&gt; toward the calling floor in the opposite direction passing it
    //   c&gt; to turn 2 (opposite of turn 1) and then back to the calling floor
    //      in the matching direction where it will be serviced
    //

    // Set turn 1 and 2 to the appropriate shaft ends
    [cabin high]
        turn 1, turn2 = top fnum, bottom fnum
    [cabin low]
        turn 1, turn2 = bottom fnum, top fnum

    // 2a&gt; Toward turn 1
    countable aslevs ..= numbered aslevs(Number: cabin floor&lt;&gt;|turn 1)
    stops += Count stops roundtrip( aslevs: countable aslevs )
    distance = cabin height.separation( Floor(turn 1).Height ) * 2 + floor gap

    // 2b&gt; From the nearest accessible shaft level to the calling floor
    countable aslevs ..= numbered aslevs(Number: cabin floor&lt;&gt;|calling fnum)
    stops += Count stops oneway( aslevs: countable aslevs, search dir: Travel direction.opposite )
    distance += cabin height.separation( calling height )

    // 2c&gt; To turn 2 and back to the calling floor
    countable aslevs ..= numbered aslevs(Number: calling floor&lt;&gt;|turn2)
    stops += Count stops roundtrip( aslevs: countable aslevs )
    distance += cabin height.separation( Floor(turn2).Height ) * 2
}

[!dir match][behind the call arrow]{
    //
    // Case 3: Cabin is moving oppsosite the calling direction behind the call
    //
    // Route:
    //
    // a&gt; toward the end of the top or bottom of shaft
    // b&gt; back twoard the call in the service direction
    // 

    // 3a&gt; To end of shaft and back to the current location
    [cabin high]
        turn = top fnum
    [cabin low]
        turn = bottom fnum

    countable aslevs ..= numbered aslevs(Number: cabin fnum&lt;&gt;turn)
    stops += Count stops roundtrip( aslevs: countable aslevs )
    distance += cabin height.separation( Floor(turn).Height ) * 2

    // 3b&gt; back toward call in service direction
    countable aslevs ..= numbered aslevs(Number: cabin fnum&lt;&gt;|calling fnum)
    stops += Count stops oneway( aslevs: countable aslevs, service dir: Travel direction.opposite)
    distance += cabin height.separation( calling floor height )
}

[!dir match][!behind the call arrow]{
    //
    // Cabin is moving in the opposite direction ahead of the call
    //
    // Route:
    // a&gt; Toward call in opposite direction
    // b&gt; Toward top or bottom of shaft and back

    // 4a&gt; Toward call in opposite direction
    countable aslevs ..= numbered aslevs(Number: cabin fnum|&lt;&gt;|calling fnum)
    stops += Count stops oneway( aslevs: countable aslevs, service dir: Travel direction.opposite)
    distance += cabin height.separation( calling floor height )

    // 4b&gt; Toward top or bottom of shaft and back
    [cabin high]
        turn = bottom fnum
    [cabin low]
        turn = top fnum

    countable aslevs ..= numbered aslevs(Number: calling floor&lt;&gt;|turn)
    stops += Count stops roundtrip( aslevs: countable aslevs )
    distance += cabin height.separation( Floor(turn).Height ) * 2
}
    
transit delay = distance / /R2/R1/Bank.Avg cabin speed
stop delay = stops * /R2/R1/Bank.Avg stop duration

=&gt;&gt; transit delay + stop delay</code></pre>

<h2>Count stops oneway</h2>

<pre><code>//
// Cabin.Count stops oneway( aslevs: Accessible Shaft Level, search dir: Direction ) : Count
//
// Returns the number of Accessible Shaft Levels to be serivced with the Cabin
// proceeding in the provided search direction
//

// Count all aslevs with a stop requested or a floor call in the search direction
//
=&gt;&gt; ?in.aslevs(Stop requested) + ?(
    in.aslevs(not Stop requested) ## Floor Service(Direction : in.search dir).(*I) )

// Note: ? is the cardinality (how many?) operator
// In a projection predicate *I is shorthand for &quot;select all primary ID attrs&quot;</code></pre>

<h2>Count stops roundtrip</h2>

<pre><code>//
// Cabin.Count stops roundtrip( aslevs: Accessible Shaft Level ) : Count
//
// Returns the number of Accessible Shaft Levels to be serviced with the
// Cabin passing in both directions

// Count all floor service calls in either direction
floor calls either dir #= in.aslevs ## Floor Service.(*I)

// Count all stop requests with no floor calls
stops without calls #= in.aslevs(Stop requested) #- floor calls either dir

// Return the total
=&gt;&gt; ?floor calls either dir + ?stops without calls</code></pre>

<h1>Bank Level Methods</h1>

<h2>Choose shaft</h2>

<pre><code>//
// Bank Level.Choose shaft( calling floor : Level Name, service dir: Direction ) : Shaft ID
//

// Invokes the Estimate delay method on all related instances of Cabin and returns
// the Shaft ID of the one with the shortest delay

// Scrall: The 1, in the restriction clause selects at most one
=&gt;&gt; /R29/R1/Shaft/R2/Cabin(1, ^-Estimate delay( calling floor: Floor, in.service dir ) ).Shaft

// A Bank must have at least one Cabin (R1/R2) and each Cabin will report some
// delay value, so a single Shaft value will always be returned</code></pre>

<h2>Floor request</h2>

<pre><code>//
// Bank Level.Floor request( dir: Direction ) : Boolean
//
// The associated subclass calling up/down attribute is set to register an up or down call.
// If floor was not already calling the Shaft most likely to provide the quickest
// service is chosen and the associated Accessible Shaft Level is notified.
// Success is returned if the floor was not already registered as calling in the
// requested service direction.
//

// Scrall: If a path from a superclass instance to a generalization relationship
// appears in a case statement, an enumerated type is generated with a value for
// each subclass name. Also, the &quot;.&quot; symbol represents the superclass instance
// for any path expressions inside the case block.
//
case ( /R38 ) {
    .Top Bank Level:
        if (in.dir == .down)
            ./Top Bank Level.Calling down.set
        else
            [error]
    .Bottom Bank Level:
        if (in.dir == .up)
            ./Bottom Bank Level.Calling up.set
        else
            [error]
    .Middle Bank Level:
        case ( in.dir ) {
            .up: ./Middle Bank Level.Calling up.set
            .down: ./Middle Bank Level.Calling down.set
        }
}

[!error] {
    // Notify the Accessible Shaft Level so that it can request service
    Floor calling( in.dir ) -&gt; 
        Accessible Shaft Level (Bank, Floor,
            Shaft: Choose shaft( calling floor: Floor, service dir: in.dir ) )
    =&gt;&gt; true

[error] =&gt;&gt; false
        </code></pre>

<h2>Clear floor request</h2>

<pre><code>//
// Bank Level.Clear floor request( dir: Direction )
//
// The subclass calling up/down request is cleared and the UI is notified.

// Scrall: the system defined _&lt;class&gt;_subclass variable is generated for
// each superclass as an enumeration of all subclass names
//
case ( _subclass name ) {
    .Top Bank Level:
        if (in.dir : .down)
            /R38/Top Bank Level.Calling down.unset
    .Bottom Bank Level:
        if (in.dir : .up)
            /R38/Bottom Bank Level.Calling up.unset
    .Middle Bank Level:
        case ( in.dir ) {
            .up: /R38/Middle Bank Level.Calling up.unset
            .down: /R38/Middle Bank Level.Calling down.unset
        }
}

   UI.Clear floor request( Bank, Floor, in.dir )</code></pre>

</body>
</html>

